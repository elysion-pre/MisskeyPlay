/// @ 0.19.0
// カスタム絵文字サークル
// Copyright (c) 2024-2025 @elysion
// This script is licensed under the MIT
// https://opensource.org/licenses/mit-license.php

let version = "1.1.0"

let PLAY_NAME = "カスタム絵文字サークル"
let PLAY_URL = `{SERVER_URL}/play/9pryd9gakh`
let PLAY_ID = if exists THIS_ID THIS_ID else PLAY_URL

let random = eval {
  let id = if exists USER_ID USER_ID else Util:uuid()
  // シードが「PLAY ID+ユーザーID+今日の日付」である乱数生成器を用意
//  Math:gen_rng(`{PLAY_ID}-{id}-{Date:year()}-{Date:month()}-{Date:day()}`)
  // シードが「PLAY ID+ユーザーID+ミリ秒」である乱数生成器を用意
  Math:gen_rng(`{PLAY_ID}-{id}-{Date:year()}-{Date:month()}-{Date:day()}-{Date:minute()}-{Date:second()}-{Date:millisecond()}`)
}

// 除外する絵文字のカテゴリ
// pagesのjsonデータ読み込み
// https://{SERVER_URL}/@elysion/pages/ignore_emoji_data
let IGNORE_EMOJI_LIST = eval {
  var data = null
  let pages_data = Mk:api("pages/show", {
    name: "ignore_emoji_data"
    username: "elysion"
  })
  if (Core:type(pages_data) == "error") {
    data = { category: [ null ] }
  } else {
    data = Json:parse(pages_data.content[0].text)
  }
  data
}

@EmojiManager() {
  let this = {
    obj: {}
    categories: []
    // コンストラクタ
    constructor: @() {
      // 除外カテゴリ以外の絵文字抽出
      this.obj = CUSTOM_EMOJIS.filter(@(emoji) {
        !IGNORE_EMOJI_LIST.category.incl(emoji.category)
      })
      // カテゴリー抽出
      var prev_category = ""
      for (let i = 0, (this.obj.len - 1)) {
        if (prev_category != this.obj[i].category) {
          this.categories.push(this.obj[i].category)
        }
        prev_category = this.obj[i].category
      }
      this
    }
    // 絵文字カテゴリを返す
    // return: <arr>
    getCategories: @() {
      this.categories
    }
    // 絵文字を返す
    // param: name<str>
    // return: <arr>
    getEmoji: @(name) {
      this.obj.filter(@(v) {
        (v.name == name)
      }).pop()
    }
    // ランダムな絵文字を返す
    // return: <obj>
    getRandomEmoji: @() {
      this.obj[random(0, (this.obj.len - 1))]
    }
    // カテゴリー内の絵文字を返す
    // param: category<str>
    // return: <arr>
    getCategoryEmojis: @(category) {
      this.obj.filter(@(emoji) {
        emoji.category == category
      })
    }
    // 絵文字のエイリアスを返す
    // param: emoji<obj>
    // return: <arr>
    getEmojiAliases: @(emoji) {
      var aliases = []
      // 絵文字追加直後はaliasesに何も入っていない場合がある
      if (emoji["aliases"].len > 0) {
        // ""の場合
        if (emoji["aliases"][0] == "") {
          // emoji.nameを返す
          aliases.push(emoji["name"])
        } else {
          // エイリアスを返す
          aliases = emoji["aliases"]
        }
      } else {
        // emoji.nameを返す
        aliases.push(emoji["name"])
      }
      aliases
    }
    // 絵文字のエイリアスを返す(fRand = trueでランダム)
    // param: emoji<obj>
    // param: fRand<bool>
    // return: <str>
    getEmojiAliase: @(emoji, fRand) {
      var aliase = ""
      var aliases = this.getEmojiAliases(emoji)
      if (fRand) {
        // ランダムでエイリアスを返す
        aliase = aliases[random(0, (aliases.len - 1))]
      } else {
        // エイリアスの先頭を返す
        aliase = aliases[0]
      }
      aliase
    }
  }
  this.constructor()
}

@Game() {
  var emojiMng = null
  var select = {
    category: ""
    category_emojis: ""
    emojis: []
  }
  var category_emojis_components = []

  // 初期化
  @init() {

    render([
      Ui:C:container({
        children: [
          Ui:C:mfm({
            text: [
              `$[x2 Now Loading]`
              `カスタム絵文字読み込み中$[bounce .]$[bounce.delay=0.2s .]$[bounce.delay=0.4s .]`
            ].join(Str:lf)
          })
        ]
        align: "center"
      })
    ])

    emojiMng = EmojiManager()
    
    render([
      Ui:C:container({
        children: [
          Ui:C:mfm({
            text: [
              `$[x2 Now Loading]`
              `絵文字ボタン作成中$[bounce .]$[bounce.delay=0.2s .]$[bounce.delay=0.4s .]`
            ].join(Str:lf)
          })
        ]
        align: "center"
      })
    ])

    // 絵文字ボタン作成
    category_emojis_components = []
    each (let category, emojiMng.getCategories()) {
      let category_emojis = emojiMng.getCategoryEmojis(category).map(@(v) {
        `$[clickable.ev={v.name} :{v.name}:]`
      })
      category_emojis_components.push(
        Ui:C:container({
          children: [
            Ui:C:folder({
              children: [
                Ui:C:container({
                  children: [
                    Ui:C:mfm({
                      text: [
                        `<small>絵文字クリックで選択</small>`
                        category_emojis.join()
                      ].join(Str:lf)
                      onClickEv: @(e) {
                        selectEmoji(e)
                        main()
                      }
                    })
                    Ui:C:button({
                      text: `【{category}】内からランダム`
                      onClick: @() {
                        selectRandomCategory(category)
                        main()
                      }
                      primary: true
                      rounded: true
                    })
                  ]
                  align: "center"
                })
              ]
              title: `{category}({category_emojis.len})`
              opened: false
            })
          ]
          align: "center"
          borderStyle: "solid"
          rounded: true
        })
      )
    }

    selectRandomAll()
    main()
  }
  // 絵柄選定: すべてランダム
  @selectRandomAll() {
    
    render([
      Ui:C:container({
        children: [
          Ui:C:mfm({
            text: [
              `$[x2 Now Loading]`
              `カスタム絵文字を読み込み中$[bounce .]$[bounce.delay=0.2s .]$[bounce.delay=0.4s .]`
            ].join(Str:lf)
          })
        ]
        align: "center"
      })
    ])

    select.emojis = []
    for 12 {
      select.emojis.push(`:{emojiMng.getRandomEmoji().name}:`)
    }
  }
  // 絵柄選定: カテゴリからランダム
  @selectRandomCategory(category) {

    render([
      Ui:C:container({
        children: [
          Ui:C:mfm({
            text: [
              `$[x2 Now Loading]`
              `【{category}】のカスタム絵文字を読み込み中$[bounce .]$[bounce.delay=0.2s .]$[bounce.delay=0.4s .]`
            ].join(Str:lf)
          })
        ]
        align: "center"
      })
    ])

    select.category = category
    // カテゴリ(ランダム)の絵文字を取得
    select.category_emojis = emojiMng.getCategoryEmojis(category)

    select.emojis = []
    for 12 {
      let emoji = select.category_emojis[random(0, (select.category_emojis.len - 1))]
      select.emojis.push(`:{emoji.name}:`)
    }
  }
  // 絵柄選定: 指定したカスタム絵文字
  @selectEmoji(emoji) {
    render([
      Ui:C:container({
        children: [
          Ui:C:mfm({
            text: [
              `$[x2 Now Loading]`
              `再作成中$[bounce .]$[bounce.delay=0.2s .]$[bounce.delay=0.4s .]`
            ].join(Str:lf)
          })
        ]
        align: "center"
      })
    ])

    select.emojis = []
    for 12 {
      select.emojis.push(`:{emoji}:`)
    }
  }
  // メイン処理
  @main() {
    // 円座標
    let CIRCLE_POS = [
      [ { x: 0.0, y: 0.5 }, { x: 0.0, y: 0.0 }, { x: 0.0, y: 0.5 } ],
      [ { x: -2.5, y: 0.0 }, { x:  2.5, y: 0.0 } ],
      [ { x: -3.0, y: 0.0 }, { x:  3.0, y: 0.0 }, ],
      [ { x: -2.5, y: 0.0 }, { x:  2.5, y: 0.0 } ],
      [ { x: 0.0, y: -0.5 }, { x: 0.0, y: 0.0 }, { x: 0.0, y: -0.5 } ]
    ]
    // 円座標に割り当て
    let circleMfm = []
    for (let i = 0, CIRCLE_POS.len) {
      for (let j = 0, CIRCLE_POS[i].len) {
        circleMfm.push(`$[position.x={CIRCLE_POS[i][j].x},y={CIRCLE_POS[i][j].y} $[spin.speed=5s,left {select.emojis[i+j]}]]`)
      }
      circleMfm.push(Str:lf)
    }

    // 結果表示用
    let resultMfm = [
      `$[spin.speed=5s {circleMfm.join()}]`
    ].join(Str:lf)

    // 投稿用テキスト
    let postText = [
      `<center>`
      resultMfm
      PLAY_URL
      `</center>`
    ].join(Str:lf)

    render([
      Ui:C:container({
        children: [
          Ui:C:mfm({
            text: resultMfm
          })
        ]
        align: "center"
      })
      Ui:C:container({
        children: [
          Ui:C:button({
            text: "もう一度"
            onClick: @() {
              selectRandomAll()
              main()
            }
          })
          Ui:C:postFormButton({
            text: "投稿する"
            form: {
              text: postText
            }
            primary: true
            rounded: true
          })
        ]
        align: "center"
      })
      Ui:C:container({
        children: [
          Ui:C:folder({
            children: [
              Ui:C:container({
                children: category_emojis_components
              })
            ]
            title: "自分でカスタム絵文字の中から選ぶ"
            opened: true
          })
        ]
        align: "center"
      })
    ])
  }
  // 描画
  // param: component<arr>
  @render(components) {
    Ui:render(components)
  }
  init()
}
Game()
