/// @ 0.19.0
// ãƒ­ãƒ¼ãƒ«ã‚‚ã©ã
// Copyright (c) 2024 @elysion
// This script is licensed under the MIT
// https://opensource.org/licenses/mit-license.php

let PLAY_URL = if exists THIS_URL THIS_URL else `{SERVER_URL}/play/a0dgdefq95`
let PLAY_ID = if exists THIS_ID THIS_ID else PLAY_URL

let random = eval {
  let id = if exists USER_ID USER_ID else Util:uuid()
  // ã‚·ãƒ¼ãƒ‰ãŒã€ŒPLAY ID+ãƒ¦ãƒ¼ã‚¶ãƒ¼ID+ä»Šæ—¥ã®æ—¥ä»˜ã€ã§ã‚ã‚‹ä¹±æ•°ç”Ÿæˆå™¨ã‚’ç”¨æ„
//  Math:gen_rng(`{PLAY_ID}-{id}-{Date:year()}-{Date:month()}-{Date:day()}`)
  // ã‚·ãƒ¼ãƒ‰ãŒã€ŒPLAY ID+ãƒ¦ãƒ¼ã‚¶ãƒ¼ID+ãƒŸãƒªç§’ã€ã§ã‚ã‚‹ä¹±æ•°ç”Ÿæˆå™¨ã‚’ç”¨æ„
  Math:gen_rng(`{PLAY_ID}-{id}-{Date:year()}-{Date:month()}-{Date:day()}-{Date:minute()}-{Date:second()}-{Date:millisecond()}`)
}

@Game() {
  var render_components = []
  var role_data = {}

  // åˆæœŸåŒ–
  @init() {
    role_data = Mk:load(`@{USER_ID}/role_data`)
    
    if (role_data == null) {
      role_data = {
        name: "ã‚¬ã‚­ç®±"
        name_color: "dadada"
        background_color: "dcdcdc"
        border_width: 1.2
        border_color: "303030"
        fBackgroundColor: false
        fProfileLink: false
      }
    }
    main()
  }
  // ãƒ©ãƒ³ãƒ€ãƒ è‰²
  // return: <str>
  @randomHex() {
    let c = `000000{Num:to_hex(random(0, 16777216))}`
    c.slice(c.len - 6, c.len)
  }
  // ãƒ¡ã‚¤ãƒ³å‡¦ç†
  @main() {
    render_components = []
    render_components.push(
      Ui:C:container({
        children: [
          Ui:C:textInput({
            onInput: @(text) {
              role_data.name = text
            }
            default: role_data.name
            label: "æ–‡å­—"
            caption: "å·¦å³ã®å¹…ã¯ã‚¹ãƒšãƒ¼ã‚¹ç­‰ã§èª¿æ•´ã—ã¦ãã ã•ã„"
          })
          Ui:C:textInput({
            onInput: @(text) {
              role_data.name_color = text.replace("#", "")
            }
            default: role_data.name_color
            label: "æ–‡å­—è‰²"
            caption: "æ—¢å®šå€¤: dadadaã€ä¾‹: ã€fffã€‘ã¾ãŸã¯ã€ffffffã€‘ã€‚è©³ã—ãã¯ã‚«ãƒ©ãƒ¼ã‚³ãƒ¼ãƒ‰ã§æ¤œç´¢"
          })
          Ui:C:numberInput({
            onInput: @(number) {
              role_data.border_width = number
            }
            default: role_data.border_width
            label: "æ ã®å¤ªã•"
            caption: "æ—¢å®šå€¤: 1.2"
          })
          Ui:C:textInput({
            onInput: @(text) {
              role_data.border_color = text.replace("#", "")
            }
            default: role_data.border_color
            label: "æ ã®è‰²"
            caption: "æ—¢å®šå€¤: 303030"
          })
          Ui:C:switch({
            onChange: @(enabled) {
              role_data.fBackgroundColor = enabled
              main()
            }
            default: role_data.fBackgroundColor
            label: "èƒŒæ™¯è‰²ã‚’ä½¿ç”¨"
            caption: "âš ï¸ONã«ã—ãªã„ã¨åæ˜ ã•ã‚Œã¾ã›ã‚“"
          })
          Ui:C:textInput({
            onInput: @(text) {
              role_data.background_color = text.replace("#", "")
            }
            default: role_data.background_color
            label: "èƒŒæ™¯è‰²"
            caption: "æ—¢å®šå€¤: dcdcdc"
          })
          Ui:C:switch({
            onChange: @(enabled) {
              if USER_USERNAME != null {
                role_data.fProfileLink = enabled
              } else {
                Mk:dialog("ã‚¨ãƒ©ãƒ¼", [
                  `ãƒ¦ãƒ¼ã‚¶ãƒ¼åãŒè¦‹ã¤ã‹ã‚Šã¾ã›ã‚“ã§ã—ãŸ`
                  `$[x2 :fushiginakotomoarumondesuna::mimi_hate:]`
                  `ãƒ­ã‚°ã‚¤ãƒ³ã—ã¦ã‹ã‚‰ONã«ã—ã¦ã¿ã¦ã­ãƒ¼ğŸ‘‹`
                ].join(Str:lf), "error")
                role_data.fProfileLink = false
              }
              main()
            }
            default: role_data.fProfileLink
            label: "è‡ªèº«ã®ãƒ—ãƒ­ãƒ•ã‚£ãƒ¼ãƒ«ãƒªãƒ³ã‚¯ã‚’åŸ‹ã‚è¾¼ã‚€"
            caption: "ä½œæˆã•ã‚ŒãŸãƒ­ãƒ¼ãƒ«ã‚‚ã©ãã‚’ã‚¿ãƒƒãƒ—ã™ã‚‹ã¨ã€è‡ªèº«ã®ãƒ—ãƒ­ãƒ•ã‚£ãƒ¼ãƒ«ã¸é·ç§»ã™ã‚‹ãƒªãƒ³ã‚¯ã«ãªã‚Šã¾ã™"
          })
          Ui:C:button({
            onClick: main
            text: "æ›´æ–°ã™ã‚‹"
            primary: true
            rounded: true
          })
          Ui:C:button({
            onClick: @() {
              if Mk:confirm("ç¢ºèª", [
                "åˆæœŸè¨­å®šã«æˆ»ã—ã¾ã™ã‹ï¼Ÿ"
                "$[x2 :majikaw::mimi_hate:]"
              ].join(Str:lf), "warning") {
                Mk:save(`@{USER_ID}/role_data`, null)
                init()
              }
            }
            text: "åˆæœŸè¨­å®šã«æˆ»ã™"
          })
          Ui:C:folder({
            children: [
              Ui:C:buttons({
                buttons: [
                  {
                    onClick: @() {
                      role_data.name_color = randomHex()
                      main()
                    }
                    text: "æ–‡å­—è‰²"
                  },
                  {
                    onClick: @() {
                      role_data.border_color = randomHex()
                      main()
                    }
                    text: "æ ã®è‰²"
                  },
                  {
                    onClick: @() {
                      role_data.background_color = randomHex()
                      if !role_data.fBackgroundColor role_data.fBackgroundColor = true
                      main()
                    }
                    text: "èƒŒæ™¯è‰²"
                  },
                  {
                    onClick: @() {
                      role_data.name_color = randomHex()
                      role_data.border_color = randomHex()
                      role_data.background_color = randomHex()
                      if !role_data.fBackgroundColor role_data.fBackgroundColor = true
                      main()
                    }
                    text: "ALL"
                  }
                ]
              })
            ]
            title: "è‰²(ãƒ©ãƒ³ãƒ€ãƒ )"
          })
        ]
        align: "center"
      })
    )
    
    var resultMfm = ""
    // fgè£…é£¾
    resultMfm = `$[fg.color={role_data.name_color}   {role_data.name}  ]`

    if role_data.fBackgroundColor {
      // bgè£…é£¾
      resultMfm = `$[bg.color={role_data.background_color} {resultMfm}]`
    }
    // borderè£…é£¾
    resultMfm = `$[border.color={role_data.border_color},width={role_data.border_width},radius=50 {resultMfm}]`

    if role_data.fProfileLink {
      // ãƒ—ãƒ­ãƒ•ã‚£ãƒ¼ãƒ«URLåŸ‹ã‚è¾¼ã¿
      resultMfm = `?[{resultMfm}]({SERVER_URL}/@{USER_USERNAME})`
    }
    
    render_components.push(
      Ui:C:container({
        children: [
          Ui:C:folder({
            children: [
              Ui:C:container({
                children: [
                  Ui:C:mfm({
                    text: resultMfm
                  })
                  Ui:C:postFormButton({
                    text: "æŠ•ç¨¿ã™ã‚‹"
                    form: {
                      text: [
                        `<center>`
                        resultMfm
                        PLAY_URL
                        `</center>`
                      ].join(Str:lf)
                    }
                    primary: true
                    rounded: true
                  })
                ]
              })
            ]
            title: "çµæœ"
          })
          Ui:C:folder({
            children: [
              Ui:C:mfm({
                text: [
                  `<small>ãƒ—ãƒ­ãƒ•ã‚£ãƒ¼ãƒ«ã«è²¼ã‚Šä»˜ã‘ã‚‹å ´åˆã¯ä¸‹è¨˜ã‚’ã‚³ãƒ”ãƒ¼</small>`
                  `\`\`\`aiscript`
                  resultMfm
                  `\`\`\``
                ].join(Str:lf)
              })
            ]
            title: "ã‚³ãƒ”ãƒ¼ç”¨"
          })
        ]
        align: "center"
      })
    )
    render(render_components)
    Mk:save(`@{USER_ID}/role_data`, role_data)
  }
  @render(components) {
    Ui:render(components)
  }
  init()
}
Game()
